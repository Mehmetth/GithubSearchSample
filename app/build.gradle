plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs'
}

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode versions.versionCode
        versionName versions.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    //BuildType has been added for illustrative purposes
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }

    //ProductFlavors has been added for illustrative purposes.
    productFlavors {
        google {
            dimension "platform"
        }
        live {
            dimension "env"
            applicationId "com.mehmetpetek.githubsample.app"
            resValue "string", "app_name", "Github Search App"
            buildConfigField("String", "BASE_URL", '"https://api.github.com/"')
        }
        dev {
            dimension "env"
            applicationId "com.mehmetpetek.githubsample.app.beta"
            resValue "string", "app_name", "Github Search App - Beta"
            buildConfigField("String", "BASE_URL", '"https://api.github.com/"')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    flavorDimensions "platform", "env"

    testOptions {
        unitTests.returnDefaultValues = true
    }
    configurations.all {
        resolutionStrategy {
            // Please removed it, as soon as the project compiles without it
            force 'org.jetbrains:annotations:20.1.0'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"

    //UI and Opr
    implementation "androidx.core:core-ktx:$versions.core"
    implementation "androidx.fragment:fragment-ktx:$versions.fragment"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraint"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.recyclerview:recyclerview:$versions.recyclerView"
    implementation "com.github.zladnrms:RoundableLayout:$versions.roudableLayout"
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    annotationProcessor "com.github.bumptech.glide:compiler:$versions.glide"

    //Async Operations
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"

    //DI
    implementation "com.google.dagger:hilt-android:$versions.hilt"
    kapt "com.google.dagger:hilt-compiler:$versions.hilt"

    //HTTP Request json convertor
    implementation "com.google.code.gson:gson:$versions.gson"

    //HTTP Request
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"

    //MemoryLeak detect
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary"

    //HTTP Request Realtime Viewer
    implementation "com.github.chuckerteam.chucker:library:$versions.chucker"

    //Room components
    implementation "androidx.room:room-runtime:$versions.room"
    kapt "androidx.room:room-compiler:$versions.room"
    implementation "androidx.room:room-ktx:$versions.room"

    //Unit Test
    testImplementation "junit:junit:$versions.junit"
    testImplementation "io.mockk:mockk:$versions.mockk"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$versions.mockito"
    testImplementation "org.robolectric:robolectric:$versions.robolectric"
    testImplementation "org.mockito:mockito-core:$versions.mockitoCore"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$versions.reflect"
    testImplementation "androidx.arch.core:core-testing:$versions.coreTesting"
    testImplementation "androidx.test:core:$versions.testCore"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$versions.coroutines"
    testImplementation "com.google.truth:truth:$versions.truth"
}